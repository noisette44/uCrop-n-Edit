apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.github.krokyze'
version = '2.2.3'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/ucropnedit-release.aar")
            groupId
            artifactId 'ucropnedit'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    override = true
    publish = true

    publications = ['Production']
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'ucropnedit'
        websiteUrl = 'https://github.com/krokyze/uCrop-n-Edit'
        vcsUrl = 'https://github.com/krokyze/uCrop-n-Edit.git'
        licenses = ["Apache-2.0"]
        
        version {
            name = "2.2.3"

            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }

            mavenCentralSync {
                sync = true
                user = properties.getProperty("bintray.oss.user")
                password = properties.getProperty("bintray.oss.password")
                close = '1'
            }
        }
    }
}